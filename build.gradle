plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.springframework.boot" version "2.5.6" apply false
}

ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("module-") }
    serviceProjects = subprojects.findAll { it.name.startsWith("service-") }
    gatewayProjects = subprojects.findAll { it.name.equalsIgnoreCase("cloud-gateway") }
}

allprojects {

    apply plugin: 'java'
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    group 'easy.cloud'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2020.0.3'
            mavenBom "org.springframework.boot:spring-boot-dependencies:2.5.6"
        }
    }

    dependencies {
        //基础依赖
        implementation 'org.apache.commons:commons-lang3:3.12.0'
        compileOnly 'org.projectlombok:lombok:1.18.22'
        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'

        //持久层依赖
        implementation 'mysql:mysql-connector-java:8.0.25'
        implementation 'com.baomidou:mybatis-plus-boot-starter:3.4.3.4'

        //服务层依赖
        implementation "org.springframework.boot:spring-boot-starter-test:2.5.6"
        implementation "org.springframework.boot:spring-boot-starter-aop:2.5.6"
    }
}

configure(serviceProjects + moduleProjects) { web ->
    dependencies {
        //web依赖
        implementation("org.springframework.boot:spring-boot-starter-web:2.5.6")
    }
}
configure(gatewayProjects) { gateway ->
    dependencies {
        //gateway依赖
        implementation 'org.springframework.cloud:spring-cloud-starter-gateway:3.0.5'
    }
}


def profile = System.getProperty("profile") ?: "dev"
def configPath = project.rootDir.getPath() + "/configs/" + profile
def dockerParentPath = project.rootDir.getPath() + "/docker/"
def dockerFileTemp = dockerParentPath + 'Dockerfile'

configure(serviceProjects + gatewayProjects) { service ->

    //SpringBoot方式打包
    apply plugin: 'org.springframework.boot'
    jar.manifest.attributes(['Main-Class': 'cloud.easy.Application'])

    dependencies {
        //微服务依赖
        implementation 'org.springframework.cloud:spring-cloud-starter-bootstrap:3.0.4'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-discovery:2021.1'
        implementation 'com.alibaba.cloud:spring-cloud-starter-alibaba-nacos-config:2021.1'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:3.0.4'
        implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer:3.0.4'
    }

    //拷贝配置文件
    def resourcesPath = projectDir.getPath() + '/build/resources/main'
    classes.doLast {
        println 'copy config file'
        copy {
            from configPath
            into resourcesPath
        }
    }

    //拷贝jar用于构建Docker
    def jarName = service.name + '-' + service.version + '.jar'
    def jarPath = projectDir.getPath() + '/build/libs/' + jarName
    def dockerPath = dockerParentPath + service.name + "/"
    bootJar.doLast {
        println 'copy jar file'
        copy {
            from jarPath
            into dockerPath
        }

        def dockerFilePath = dockerPath + 'Dockerfile'
        def list = []
        new File(dockerFileTemp).withReader('UTF-8') { reader ->
            reader.eachLine {
                it = it.replace('${jar}', jarName)
                it = it.replace('${profile}', profile)
                list.add(it + '\r\n')
            }
        }
        println 'write Dockerfile'
        new File(dockerFilePath).withWriter('UTF-8') { writer ->
            list.each {
                writer.write(it)
            }
        }
    }
}


